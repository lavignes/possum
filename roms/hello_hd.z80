HD_PORT_DATA:		equ $80
HD_PORT_ERR:		equ $81
HD_PORT_FEAT:		equ $81
HD_PORT_SEC_CNT:	equ $82
HD_PORT_SEC_NUM:	equ $83
HD_PORT_CYL_LO:		equ $84
HD_PORT_CYL_HI:		equ $85
HD_PORT_HEAD:		equ $86
HD_PORT_STAT:		equ $87
HD_PORT_CMD:		equ $87

HD_FEAT_8BIT_ON:	equ $01
HD_STAT_BSY:		equ $80
HD_DRIVE0:		equ $e0
HD_DRIVE1:		equ $f0

HD_CMD_DIAG:		equ $90
HD_CMD_ERASE:		equ $c0
HD_CMD_IDENT:		equ $ec
HD_CMD_NOP:		equ $00
HD_CMD_RD:		equ $20
HD_CMD_FEAT:		equ $ef
HD_CMD_WR:		equ $30

HD_IDENT_SERIAL:	equ $14
HD_IDENT_SERIAL_LEN:	equ 20
HD_IDENT_MODEL:		equ $36
HD_IDENT_MODEL_LEN:	equ 40
HD_IDENT_SIZE:		equ $0e

PIPE_PORT: equ $f0

	org $0000
start:
	ld sp, $ffff

	call hd_8bit
	call c, err
	call hd_ident
	call c, err

	ld hl, HD_MSG_MODEL
	ld b, HD_MSG_MODEL_END - HD_MSG_MODEL
	call pip_wr

	ld hl, SECTOR_BUF + HD_IDENT_MODEL
	ld b, HD_IDENT_MODEL_LEN
	call pip_wrln

	ld hl, HD_MSG_SERIAL
	ld b, HD_MSG_SERIAL_END - HD_MSG_SERIAL
	call pip_wr

	ld hl, SECTOR_BUF + HD_IDENT_SERIAL
	ld b, HD_IDENT_SERIAL_LEN
	call pip_wrln

	ld hl, HD_MSG_SIZE
	ld b, HD_MSG_SIZE_END - HD_MSG_SIZE
	call pip_wr

	ld a, '$'
	call pip_wrc
	ld de, SECTOR_BUF + HD_IDENT_SIZE + 3
	ld a, (de)
	call pip_wrx
	dec de
	ld a, (de)
	call pip_wrx
	dec de
	ld a, (de)
	call pip_wrx
	dec de
	ld a, (de)
	call pip_wrx
	ld a, '\r'
	call pip_wrc
	ld a, '\n'
	call pip_wrc

	ld de, $0000
	ld hl, $0000
	call hd_lba
	call hd_rd
	jr c, err

	ld hl, HELLO_MSG
	ld de, SECTOR_BUF
	ld b, HELLO_MSG_END - HELLO_MSG
.cpy:
	ld a, (hl)
	ex de, hl
	ld (hl), a
	ex de, hl
	inc hl
	inc de
	djnz .cpy

	ld de, $0000
	ld hl, $0000
	call hd_lba
	call hd_wr
	jr c, err

	ld hl, HELLO_MSG
	ld b, HELLO_MSG_END - HELLO_MSG
	call pip_wrln

	jr exit

err:
	push af
	ld hl, HD_ERR_MSG
	ld b, HD_ERR_MSG_END - HD_ERR_MSG
	call pip_wr
	ld a, '$'
	pop af
	call pip_wrx

exit:
	halt

hd_8bit:
	call hd_wait
	ld c, HD_PORT_FEAT
	ld a, HD_FEAT_8BIT_ON
	out (c), a
	ld c, HD_PORT_CMD
	ld a, HD_CMD_FEAT
	out (c), a
	jp hd_err

hd_ident:
	call hd_wait
	ld c, HD_PORT_CMD
	ld a, HD_CMD_IDENT
	out (c), a
	call hd_err
	ret c
	jp hd_bufin

hd_rd:
	call hd_wait
	ld c, HD_PORT_CMD
	ld a, HD_CMD_RD
	out (c), a
	call hd_err
	ret c
	jp hd_bufin

hd_wr:
	call hd_wait
	ld c, HD_PORT_CMD
	ld a, HD_CMD_WR
	out (c), a
	call hd_err
	ret c
	jp hd_bufout

hd_lba:
	ld c, HD_PORT_SEC_NUM
	out (c), e
	inc c
	out (c), d
	inc c
	out (c), l
	inc c
	ld a, h
	and $0f
	or HD_DRIVE0
	out (c), a
	ret

hd_bufin:
	ld de, 512
	ld hl, SECTOR_BUF
.in_byt:
	call hd_wait
	ld c, HD_PORT_DATA
	in a, (c)
	ld (hl), a
	inc hl
	dec de
	ld a, d
	or e
	jp nz, .in_byt
	jp hd_err

hd_bufout:
	ld de, 512
	ld hl, SECTOR_BUF
.ot_byt:
	call hd_wait
	ld c, HD_PORT_DATA
	ld a, (hl)
	out (c), a
	inc hl
	dec de
	ld a, d
	or e
	jp nz, .ot_byt
	jp hd_err

hd_err:
	call hd_wait
	ld c, HD_PORT_STAT
	in a, (c)
	bit 0, a
	ret z
	scf
	ld c, HD_PORT_ERR
	in a, (c)
	ret

hd_wait:
	ld c, HD_PORT_STAT
	ld b, HD_STAT_BSY
.bsy_wt:
	in a, (c)
	and b
	jr nz, .bsy_wt
	ret

HEX_DIGITS:
	db "0123456789abcdef"

pip_wrx:
	push af
	ld c, a
	srl c
	srl c
	srl c
	srl c
	xor b, b
	ld hl, HEX_DIGITS
	add hl, bc
	ld c, PIPE_PORT
	ld a, (hl)
	out (c), a
	pop af
	and $0f
	ld c, a
	xor b, b
	ld hl, HEX_DIGITS
	add hl, bc
	ld c, PIPE_PORT
	ld a, (hl)
	out (c), a
	ret

pip_wrc:
	ld c, PIPE_PORT
	out (c), a
	ret

pip_wr:
	ld c, PIPE_PORT
.nbyte:
	ld a, (hl)
	out (c), a
	inc hl
	djnz .nbyte
	ret

pip_wrln:
	call pip_wr
	ld a, '\r'
	out (c), a
	ld a, '\n'
	out (c), a
	ret

HELLO_MSG:
	db "Hello! Successfully wrote to the disk!"
HELLO_MSG_END:

HD_MSG_MODEL:
	db "Model Name:\t"
HD_MSG_MODEL_END:

HD_MSG_SERIAL:
	db "Serial #:\t"
HD_MSG_SERIAL_END:

HD_MSG_SIZE:
	db "Disk Size:\t"
HD_MSG_SIZE_END:

HD_ERR_MSG:
	db "Disk error:\t"
HD_ERR_MSG_END:

	org $A000
SECTOR_BUF:
	org $A100
SECTOR_BUF_END:
